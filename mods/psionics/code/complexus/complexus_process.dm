/datum/psi_complexus/proc/update(force)

	set waitfor = FALSE

	var/last_rating = rating
	var/highest_faculty
	var/highest_rank = 0
	var/combined_rank = 0
	for(var/faculty in ranks)
		var/check_rank = get_rank(faculty)
		if(check_rank == 1)
			LAZYADD(latencies, faculty)
		else
			if(check_rank <= 0)
				ranks -= faculty
			LAZYREMOVE(latencies, faculty)
		combined_rank += check_rank
		if(!highest_faculty || highest_rank < check_rank)
			highest_faculty = faculty
			highest_rank = check_rank

	UNSETEMPTY(latencies)
	var/rank_count = max(1, LAZYLEN(ranks))
	if(force || last_rating != ceil(combined_rank/rank_count))
		if(highest_rank <= 1)
			if(highest_rank == 0)
				qdel(src)
			return
		else
			rebuild_power_cache = TRUE
			sound_to(owner, 'sound/effects/psi/power_unlock.ogg')
			rating = ceil(combined_rank/rank_count)
			cost_modifier = 1
			if(rating > 1)
				cost_modifier -= min(1, max(0.1, (rating-1) / 10))
			if(!ui)
				ui = new(owner)

/*				if(owner.client)
					owner.client.screen += ui.components
					owner.client.screen += ui
			else
				if(owner.client)
					owner.client.screen |= ui.components
					owner.client.screen |= ui*/
//FD PSIONICS//
			for(var/faculty in ranks)
				var/existing_button = FALSE
				for(var/obj/screen/psi/toggle_faculty/button in ui.components)
					if(button.faculty_id == faculty)
						existing_button = button
				if(ranks[faculty] <= PSI_RANK_LATENT)
					if(existing_button)
						ui.components -= existing_button
						qdel(existing_button)
					continue
				if(existing_button)
					continue
				var/obj/screen/psi/toggle_faculty/faculty_toggle = new(owner, faculty)
				ui.components.Insert(2, faculty_toggle)
				var/obj/screen/psi/toggle_psi_menu/arrow = ui.components[LAZYLEN(ui.components)]
				faculty_toggle.hidden = arrow.hidden
			if(owner.client)
				owner.client.screen |= ui.components
				owner.client.screen |= ui
			if(!suppressed && owner.client)
				for(var/thing in SSpsi.all_aura_images)
					owner.client.images |= thing
			ui.update_icon()
//FD PSIONICS//

			var/image/aura_image = get_aura_image()
			if(rating >= PSI_RANK_GRANDMASTER) // spooky boosters
				aura_color = "#000000"
				aura_image.blend_mode = BLEND_ADD
			else
				aura_image.blend_mode = BLEND_ADD
				switch(highest_faculty)
					if(PSI_COERCION)
						aura_color = "#3333cc"
					if(PSI_PSYCHOKINESIS)
						aura_color = "#cc3333"
					if(PSI_REDACTION)
						aura_color = "#33cc33"
					if(PSI_ENERGISTICS)
						aura_color = "#cc8221"
					if(PSI_CONSCIOUSNESS)
						aura_color = "#5233cc"
					if(PSI_METAKINESIS)
						aura_color = "#cccc33"
					if(PSI_MANIFESTATION)
						aura_color = "#cc8221"
			aura_image.pixel_x = -owner.default_pixel_x
			aura_image.pixel_y = -owner.default_pixel_y

	if(!announced && owner && owner.client && !QDELETED(src))
		announced = TRUE
		to_chat(owner, "<hr>")
		to_chat(owner, SPAN_NOTICE(FONT_LARGE("You are <b>psionic</b>, touched by powers beyond understanding.")))
		to_chat(owner, SPAN_NOTICE("<b>Shift-left-click your Psi icon</b> on the bottom right to <b>view a summary of how to use them</b>, or <b>left click</b> it to <b>suppress or unsuppress</b> your psionics. Beware: overusing your gifts can have <b>deadly consequences</b>."))
		to_chat(owner, "<hr>")

/datum/psi_complexus/Process()
	var/update_hud
	if(armor_cost)
		var/value = max(1, ceil(armor_cost * cost_modifier))
		if(value <= stamina)
			stamina -= value
		else
			backblast(abs(stamina - value))
			stamina = 0
		update_hud = TRUE
		armor_cost = 0

	if(stun)
		stun--
		if(stun)
			if(!suppressed)
				suppressed = TRUE
				update_hud = TRUE
		else
			to_chat(owner, SPAN_NOTICE("You have recovered your mental composure."))
			update_hud = TRUE
	else
		var/psi_leech = owner.do_psionics_check()
		if(psi_leech)
			if(stamina > 10)
				stamina = max(0, stamina - rand(15,20))
				//to_chat(owner, SPAN_DANGER("You feel your psi-power leeched away by \the [psi_leech]..."))
			else
				stamina++
		else if(stamina < max_stamina)
			if(owner.stat == CONSCIOUS)
				stamina = min(max_stamina, stamina + rand(1,3))
			else if(owner.stat == UNCONSCIOUS)
				stamina = min(max_stamina, stamina + rand(3,5))

		if(!owner.nervous_system_failure() && stamina && !suppressed && get_rank(PSI_REDACTION) >= PSI_RANK_APPRENTICE)
			if(get_rank(PSI_REDACTION) >= PSI_RANK_GRANDMASTER)
				attempt_regeneration()

	var/next_aura_alpha = suppressed ? 0 : clamp(round((rating/5)*255), 0, 255)

	// Перезапускаем пульсацию только при изменении параметров
	if(next_aura_alpha != last_aura_alpha || aura_color != last_aura_color)
		last_aura_alpha = next_aura_alpha
		last_aura_color = aura_color
		start_aura_pulse()

	if(update_hud)
		ui.update_icon()

/**
 * Запускает плавную бесконечную анимацию "дыхания" ауры.
 *
 * Слабый псионик: медленно, тускло, почти незаметный пульс.
 * Сильный псионик: быстро, ярко, выраженное расширение.
 * При подавлении: плавно гаснет.
 */
/datum/psi_complexus/proc/start_aura_pulse()
	var/image/I = get_aura_image()
	if(!I) return

	if(suppressed)
		animate(I, alpha = 0, time = 25)
		return

	var/r = clamp(rating, 2, 5)

	// Скорость: rating 2 → 70 тиков/полцикл (7 сек), rating 5 → 22 тиков (2.2 сек)
	var/pulse_half = clamp(round(140 / r), 22, 70)

	// Яркость: аура никогда не гаснет полностью — тускнеет до alpha_min
	var/alpha_min = clamp(r * 9, 15, 45)
	var/alpha_max = clamp(r * 36, 60, 160)

	// Масштаб: фиксированный max 1.10, min зависит от rating
	var/scale_min = 0.65
	var/scale_max = min(0.75 + (r / 20), 1)

	// Установить цвет и начальное состояние (стартуем с alpha_min, не с 0)
	I.color = aura_color
	I.alpha = alpha_min
	I.transform = matrix().Update(scale_x = scale_min, scale_y = scale_min)

	// Вдох: выходим на полную яркость и размер. loop = -1 = бесконечная цепочка
	animate(I,
		alpha     = alpha_max,
		transform = matrix().Update(scale_x = scale_max, scale_y = scale_max),
		time      = pulse_half,
		loop      = -1
	)
	// Выдох: цепляется к предыдущему кадру (БЕЗ объекта — это ключевой момент!)
	animate(
		alpha     = alpha_min,
		transform = matrix().Update(scale_x = scale_min, scale_y = scale_min),
		time      = pulse_half
	)

/datum/psi_complexus/proc/attempt_regeneration()

	var/heal_general =  FALSE
	var/heal_poison =   FALSE
	var/heal_internal = FALSE
	var/heal_bleeding = FALSE
	var/heal_rate =     0
	var/mend_prob =     0

	var/use_rank = get_rank(PSI_REDACTION)
	switch(use_rank)
		if(PSI_RANK_GRANDMASTER)
			heal_general = TRUE
			heal_poison = TRUE
			heal_internal = TRUE
			heal_bleeding = TRUE
			mend_prob = 100
			heal_rate = 10
		if(PSI_RANK_MASTER)
			heal_general = TRUE
			heal_poison = TRUE
			heal_internal = TRUE
			heal_bleeding = TRUE
			mend_prob = 80
			heal_rate = 7
		if(PSI_RANK_OPERANT)
			heal_poison = TRUE
			heal_internal = TRUE
			heal_bleeding = TRUE
			mend_prob = 60
			heal_rate = 5
		if(PSI_RANK_APPRENTICE)
			heal_internal = TRUE
			heal_bleeding = TRUE
			mend_prob = 40
			heal_rate = 5
		else
			return

	if(!heal_rate || stamina < heal_rate)
		return // Don't backblast from trying to heal ourselves thanks.

	if(ishuman(owner))

		var/mob/living/carbon/human/H = owner

		// Fix some pain.
		if(heal_rate > 0)
			H.shock_stage = max(0, H.shock_stage - max(1, round(heal_rate/2)))

		// Mend internal damage.
		if(prob(mend_prob))

			// Fix our heart if we're paramount.
			if(heal_general && H.is_asystole() && H.should_have_organ(BP_HEART) && spend_power(heal_rate))
				H.resuscitate()

			// Heal organ damage.
			if(heal_internal)
				for(var/obj/item/organ/I in H.internal_organs)

					if(BP_IS_ROBOTIC(I) || BP_IS_CRYSTAL(I))
						continue

					if(I.damage > 0 && spend_power(heal_rate))
						I.damage = max(I.damage - heal_rate, 0)
						if(prob(25))
							to_chat(H, SPAN_NOTICE("Your innards itch as your autoredactive faculty mends your [I.name]."))
						return

			// Heal broken bones.
			if(LAZYLEN(H.bad_external_organs))
				for(var/obj/item/organ/external/E in H.bad_external_organs)

					if(BP_IS_ROBOTIC(E))
						continue

					if(heal_internal && (E.status & ORGAN_BROKEN) && E.damage < (E.min_broken_damage * config.organ_health_multiplier)) // So we don't mend and autobreak.
						if(spend_power(heal_rate))
							if(E.mend_fracture())
								to_chat(H, SPAN_NOTICE("Your autoredactive faculty coaxes together the shattered bones in your [E.name]."))
								return

					if(heal_bleeding)

						if((E.status & ORGAN_ARTERY_CUT) && spend_power(heal_rate))
							to_chat(H, SPAN_NOTICE("Your autoredactive faculty mends the torn artery in your [E.name], stemming the worst of the bleeding."))
							E.status &= ~ORGAN_ARTERY_CUT
							return

						if(E.status & ORGAN_TENDON_CUT)
							to_chat(H, SPAN_NOTICE("Your autoredactive faculty repairs the severed tendon in your [E.name]."))
							E.status &= ~ORGAN_TENDON_CUT
							return TRUE

						for(var/datum/wound/W in E.wounds)

							if(W.bleeding() && spend_power(heal_rate))
								to_chat(H, SPAN_NOTICE("Your autoredactive faculty knits together severed veins, stemming the bleeding from \a [W.desc] on your [E.name]."))
								W.bleed_timer = 0
								W.clamped = TRUE
								E.status &= ~ORGAN_BLEEDING
								return

	// Heal radiation, cloneloss and poisoning.
	if(heal_poison)

		if(owner.radiation && spend_power(heal_rate))
			if(prob(25))
				to_chat(owner, SPAN_NOTICE("Your autoredactive faculty repairs some of the radiation damage to your body."))
			owner.radiation = max(0, owner.radiation - heal_rate)
			return

		if(owner.getCloneLoss() && spend_power(heal_rate))
			if(prob(25))
				to_chat(owner, SPAN_NOTICE("Your autoredactive faculty stitches together some of your mangled DNA."))
			owner.adjustCloneLoss(-heal_rate)
			return

	// Heal everything left.
	if(heal_general && prob(mend_prob) && (owner.getBruteLoss() || owner.getFireLoss() || owner.getOxyLoss()) && spend_power(heal_rate))
		owner.adjustBruteLoss(-(heal_rate))
		owner.adjustFireLoss(-(heal_rate))
		owner.adjustOxyLoss(-(heal_rate))
		if(prob(25))
			to_chat(owner, SPAN_NOTICE("Your skin crawls as your autoredactive faculty heals your body."))
	owner.update_icon()
